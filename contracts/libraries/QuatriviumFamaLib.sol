// SPDX-License-Identifier: MIT
pragma solidity ^0.8.24;

import {IERC20} from "@openzeppelin/contracts/token/ERC20/IERC20.sol";
import {SafeERC20} from "@openzeppelin/contracts/token/ERC20/utils/SafeERC20.sol";
import {AggregatorV3Interface} from "../interfaces/AggregatorV3Interface.sol";
import {IQuatriviumFamaCaja} from "../interfaces/IQuatriviumFamaCaja.sol";
import {Usuario} from "./Usuario.sol";

interface ICreditDisp {
    function dispersionCongelada(address usuario) external view returns (bool);
}

/**
 * @title QuatriviumFamaLib
 * @notice Ganchos y peg. El bytecode de la librería no cuenta para EIP-170 del núcleo.
 */
library QuatriviumFamaLib {
    using SafeERC20 for IERC20;

    uint256 internal constant BONO_ACTIVACION = 1e18;
    uint256 internal constant POOL_FLOOR_BP = 4000;
    uint256 internal constant FUNDADOR_BP = 1500;
    uint256 internal constant PISO_CAJA_BP = 2000;
    uint8 internal constant MAX_LINEA = 40;
    uint256 internal constant CARRIL_REF_BP = 1000;
    uint256 internal constant CARRIL_CHICO_PISO = 50e18;
    uint256 internal constant UTIL_GRANDE_NUM = 7000;
    uint256 internal constant UTIL_GRANDE_DEN = 8000;

    /// @notice Chico si el préstamo no pasa de max(50 USDT, 0,5 % de caja general).
    function umbralCarrilChico(uint256 caja) public pure returns (uint256 u) {
        u = (caja * CARRIL_REF_BP) / 200000;
        if (u < CARRIL_CHICO_PISO) u = CARRIL_CHICO_PISO;
    }

    function esCarrilChico(uint256 caja, uint256 monto) public pure returns (bool) {
        return monto > 0 && monto <= umbralCarrilChico(caja);
    }

    function liqGeneral(uint256 totalLiq, uint256 reservado) public pure returns (uint256) {
        return totalLiq > reservado ? totalLiq - reservado : 0;
    }

    /// @notice Hitos cada 100 hasta 1000: ~mitad del interés de ese préstamo (no 30 000 fijos).
    function bonoDeHito(uint256 hito) external pure returns (uint256) {
        if (hito == 100) return 400e18;
        if (hito == 200) return 4000e18;
        if (hito == 300) return 7000e18;
        if (hito == 400) return 8500e18;
        if (hito == 500) return 9000e18;
        if (hito == 600) return 11500e18;
        if (hito == 700) return 13500e18;
        if (hito == 800) return 16000e18;
        if (hito == 900) return 18000e18;
        if (hito == 1000) return 20000e18;
        return 0;
    }

    /// @notice Los grandes paran al 70 % si el tope global es 80 % (misma proporción si el admin mueve el 80 %).
    function topeUtilGrande(uint256 utilMaxBps) public pure returns (uint256) {
        return (utilMaxBps * UTIL_GRANDE_NUM) / UTIL_GRANDE_DEN;
    }

    function tasaCurva(
        uint256 utilBP,
        uint256 baseBP,
        uint256 optimoBP,
        uint256 pendiente1,
        uint256 pendiente2
    ) public pure returns (uint256) {
        if (utilBP < 1) return baseBP;
        if (utilBP <= optimoBP) {
            if (optimoBP < 1) return baseBP;
            return baseBP + (pendiente1 * utilBP) / optimoBP;
        }
        uint256 extraBP = utilBP - optimoBP;
        uint256 denom = 10000 - optimoBP;
        if (denom < 1) return baseBP + pendiente1 + pendiente2;
        return baseBP + pendiente1 + (pendiente2 * extraBP) / denom;
    }

    function usaSelloPrimera(
        uint256 reserved,
        uint256 expira,
        uint256 ts,
        uint256 monto,
        uint256 nivel,
        bool nuncaPidio
    ) public pure returns (bool) {
        return nuncaPidio && nivel == 1 && monto == BONO_ACTIVACION && reserved >= BONO_ACTIVACION && ts <= expira;
    }

    function requireAcceso(
        uint256 totalLiq,
        uint256 reservado,
        uint256 outstanding,
        uint256 monto,
        uint256 utilMaxBps,
        bool esChico
    ) internal pure {
        require(monto > 0);
        uint256 base = liqGeneral(totalLiq, reservado);
        require(base >= outstanding + monto, "insufficient liquidity");
        uint256 utilAfter = ((outstanding + monto) * 10000) / base;
        if (!esChico) {
            require(utilAfter <= topeUtilGrande(utilMaxBps), "insufficient liquidity");
        }
        require(utilAfter <= utilMaxBps, "utilization cap");
    }

    function liberarSelloSiExpiro(
        mapping(address => uint256) storage reservaPrimera,
        mapping(address => uint256) storage reservaPrimeraExpira,
        mapping(address => address) storage reservaPrimeraToken,
        mapping(address => uint256) storage totalReservadoPrimera,
        address tokenHint,
        address user
    ) external {
        uint256 r = reservaPrimera[user];
        if (r == 0 || block.timestamp <= reservaPrimeraExpira[user]) return;
        address tkn = reservaPrimeraToken[user];
        if (tkn == address(0)) tkn = tokenHint;
        reservaPrimera[user] = 0;
        reservaPrimeraExpira[user] = 0;
        reservaPrimeraToken[user] = address(0);
        uint256 tot = totalReservadoPrimera[tkn];
        totalReservadoPrimera[tkn] = tot > r ? tot - r : 0;
    }

    function pagarActivacion(
        mapping(address => uint256) storage totalLiquidity,
        mapping(address => uint256) storage reputacion,
        IERC20 token,
        address tokenAddr,
        address padre,
        address deudor,
        bool padreOk,
        uint256 caja,
        uint256 piso,
        address hermano
    ) public returns (uint256 bono) {
        if (hermano != address(0) && padreOk && padre != address(0) && padre != deudor) {
            (bool okA, bytes memory rawA) = hermano.staticcall(abi.encodeWithSignature("alta()"));
            if (okA && rawA.length >= 32) {
                address alta = abi.decode(rawA, (address));
                if (alta != address(0)) {
                    (bool ok, bytes memory data) = alta.call(
                        abi.encodeWithSignature("soltarPadrino(address,address)", deudor, padre)
                    );
                    if (ok && data.length >= 32) {
                        bono = abi.decode(data, (uint256));
                        if (bono > 0) return bono;
                    }
                    return 0;
                }
            }
        }
        return pagarBonoPool(
            totalLiquidity,
            reputacion,
            token,
            tokenAddr,
            padre,
            deudor,
            padreOk,
            caja,
            piso,
            hermano
        );
    }

    function sellarPrimera(
        mapping(address => uint256) storage reservaPrimera,
        mapping(address => uint256) storage reservaPrimeraExpira,
        mapping(address => address) storage reservaPrimeraToken,
        mapping(address => uint256) storage totalReservadoPrimera,
        uint256 yaPidioTimestamp,
        address token,
        address user,
        uint256 selloDuracion
    ) public {
        if (yaPidioTimestamp != 0) return;
        uint256 r = reservaPrimera[user];
        if (r != 0 && block.timestamp <= reservaPrimeraExpira[user]) return;
        if (r != 0) {
            address tknOld = reservaPrimeraToken[user];
            if (tknOld == address(0)) tknOld = token;
            uint256 totOld = totalReservadoPrimera[tknOld];
            totalReservadoPrimera[tknOld] = totOld > r ? totOld - r : 0;
            reservaPrimera[user] = 0;
            reservaPrimeraExpira[user] = 0;
            reservaPrimeraToken[user] = address(0);
        }
        reservaPrimera[user] = BONO_ACTIVACION;
        reservaPrimeraExpira[user] = block.timestamp + selloDuracion;
        reservaPrimeraToken[user] = token;
        totalReservadoPrimera[token] += BONO_ACTIVACION;
    }

    function consumirSello(
        mapping(address => uint256) storage reservaPrimera,
        mapping(address => uint256) storage reservaPrimeraExpira,
        mapping(address => address) storage reservaPrimeraToken,
        mapping(address => uint256) storage totalReservadoPrimera,
        address token,
        address user
    ) external {
        uint256 r = reservaPrimera[user];
        address tkn = reservaPrimeraToken[user];
        reservaPrimera[user] = 0;
        reservaPrimeraExpira[user] = 0;
        reservaPrimeraToken[user] = address(0);
        if (tkn == address(0)) tkn = token;
        uint256 tot = totalReservadoPrimera[tkn];
        totalReservadoPrimera[tkn] = tot > r ? tot - r : 0;
    }

    event BonoRedPagado(address indexed usuario, uint256 umbral, uint256 monto, address indexed token);
    event ReputationUpdated(address indexed usuario, uint256 nuevaReputacion);
    event ComisionGeneracional(
        address indexed beneficiario,
        address indexed deudor,
        uint8 generacion,
        uint256 monto,
        address indexed token
    );
    event BonoActivacionPagado(address indexed padre, address indexed referido, uint256 monto, address indexed token);
    event InteresRetenidoPool(address indexed token, uint256 monto);

    /// @notice Donar y aportar: 100 fama por cada 1 USDT, desde el primero.
    function ptsDonacion(uint256, uint256 amount) external pure returns (uint256) {
        if (amount == 0) return 0;
        return (amount * 100) / 1e18;
    }

    function pagarBonosRed(
        mapping(address => uint256) storage puntosRed,
        mapping(address => uint256) storage bonosRedCobrados,
        mapping(address => uint256) storage totalLiquidity,
        mapping(address => uint256) storage outstandingLoans,
        IERC20 pago,
        address token,
        address usuario,
        uint256 umbral,
        uint256 bonoUsdt,
        uint256 pisoBp
    ) external {
        uint256 earned = puntosRed[usuario] / umbral;
        uint256 already = bonosRedCobrados[usuario];
        if (earned <= already) return;
        uint256 n = earned - already;
        if (n > 20) n = 20;
        for (uint256 i = 0; i < n; i++) {
            uint256 liq = totalLiquidity[token];
            uint256 out = outstandingLoans[token];
            uint256 caja = liq > out ? liq - out : 0;
            uint256 piso = (liq * pisoBp) / 10000;
            if (caja <= piso || caja - piso < bonoUsdt) break;
            totalLiquidity[token] = liq - bonoUsdt;
            pago.safeTransfer(usuario, bonoUsdt);
            already += 1;
            emit BonoRedPagado(usuario, already * umbral, bonoUsdt, token);
        }
        bonosRedCobrados[usuario] = already;
    }

    function tocar(address hermano, address who, uint256 pts) external {
        if (hermano == address(0) || who == address(0)) return;
        if (pts == 0) {
            IQuatriviumFamaCaja(hermano).wipe(who);
            return;
        }
        IQuatriviumFamaCaja(hermano).acreditar(who, pts);
    }

    function onCierre(address hermano, address deudor, address padre) external {
        if (hermano == address(0) || deudor == address(0) || padre == address(0) || padre == deudor) return;
        IQuatriviumFamaCaja(hermano).onCierrePrestamo(deudor, padre);
    }

    function sacarCaja(
        mapping(address => uint256) storage totalLiquidity,
        IERC20 token,
        address tokenAddr,
        address to,
        uint256 amount,
        uint256 caja,
        uint256 piso
    ) external {
        require(to != address(0) && to != address(this) && amount > 0);
        require(caja > piso && caja - piso >= amount);
        require(token.balanceOf(address(this)) >= amount);
        totalLiquidity[tokenAddr] -= amount;
        token.safeTransfer(to, amount);
    }

    function pagarBonoPool(
        mapping(address => uint256) storage totalLiquidity,
        mapping(address => uint256) storage reputacion,
        IERC20 token,
        address tokenAddr,
        address padre,
        address deudor,
        bool padreOk,
        uint256 caja,
        uint256 piso,
        address hermano
    ) public returns (uint256 bono) {
        if (!padreOk || padre == address(0) || padre == deudor) {
            return 0;
        }
        bono = BONO_ACTIVACION;
        if (caja <= piso || caja - piso < bono) return 0;
        totalLiquidity[tokenAddr] -= bono;
        token.safeTransfer(padre, bono);
        emit ComisionGeneracional(padre, deudor, 1, bono, tokenAddr);
        reputacion[padre] += 100;
        emit ReputationUpdated(padre, reputacion[padre]);
        if (hermano != address(0)) {
            IQuatriviumFamaCaja(hermano).acreditarFamaRed(deudor);
        }
    }

    function bpGeneracion(uint8 generacion) public pure returns (uint256) {
        if (generacion == 1) return 1500;
        if (generacion == 2) return 800;
        if (generacion == 3) return 600;
        if (generacion == 4) return 400;
        if (generacion == 5) return 200;
        if (generacion <= 12) return 80;
        return 40;
    }

    function dispersarInteres(
        mapping(address => Usuario) storage red,
        mapping(address => bool) storage blacklist,
        mapping(address => uint256) storage reputacion,
        mapping(address => uint256) storage totalLiquidity,
        mapping(address => uint256) storage outstandingLoans,
        IERC20 pago,
        address token,
        address deudor,
        address fundador,
        uint256 interes,
        uint256 nivelPrestamo,
        uint256 pagosAntes,
        address hermano
    ) external returns (uint256 comisionesRed, uint256 retenidoPool, bool usoBonoA) {
        if (interes == 0) return (0, 0, false);
        bool habriaBonoA = pagosAntes == 0 && nivelPrestamo == 1 && !red[deudor].bonoActivacionCobrado;
        uint256 remaining = interes;
        uint256 corte = (interes * FUNDADOR_BP) / 10000;
        remaining -= _pagarCapped(blacklist, pago, token, fundador, corte, remaining, deudor, 0);
        address padre = red[deudor].padre;
        if (habriaBonoA) {
            uint256 liq = totalLiquidity[token];
            uint256 out = outstandingLoans[token];
            uint256 caja = liq > out ? liq - out : 0;
            uint256 piso = (liq * PISO_CAJA_BP) / 10000;
            red[deudor].bonoActivacionCobrado = true;
            uint256 pagadoBono = pagarActivacion(
                totalLiquidity,
                reputacion,
                pago,
                token,
                padre,
                deudor,
                padre != address(0) && !ICreditDisp(address(this)).dispersionCongelada(padre),
                caja,
                piso,
                hermano
            );
            if (pagadoBono > 0) {
                usoBonoA = true;
                emit BonoActivacionPagado(padre, deudor, pagadoBono, token);
            } else {
                red[deudor].bonoActivacionCobrado = false;
            }
        }
        uint256 floor = (interes * POOL_FLOOR_BP) / 10000;
        address cursor = padre;
        for (uint8 gen = 1; gen <= MAX_LINEA; gen++) {
            if (cursor == address(0) || cursor == deudor || remaining == 0) break;
            if (!(usoBonoA && gen == 1)) {
                uint256 share = (interes * bpGeneracion(gen)) / 10000;
                if (gen >= 6) {
                    if (remaining <= floor) break;
                    uint256 room = remaining - floor;
                    if (share > room) share = room;
                }
                remaining -= _pagarCapped(blacklist, pago, token, cursor, share, remaining, deudor, gen);
            }
            cursor = red[cursor].padre;
        }
        retenidoPool = remaining;
        comisionesRed = interes - retenidoPool;
        if (retenidoPool > 0) {
            totalLiquidity[token] += retenidoPool;
            emit InteresRetenidoPool(token, retenidoPool);
        }
    }

    function _pagarCapped(
        mapping(address => bool) storage blacklist,
        IERC20 pago,
        address token,
        address to,
        uint256 amount,
        uint256 remaining,
        address deudor,
        uint8 generacion
    ) private returns (uint256) {
        if (remaining == 0 || amount == 0) return 0;
        if (amount > remaining) amount = remaining;
        if (amount == 0 || to == address(0) || to == deudor || blacklist[to]) return 0;
        if (generacion != 0 && ICreditDisp(address(this)).dispersionCongelada(to)) return 0;
        pago.safeTransfer(to, amount);
        emit ComisionGeneracional(to, deudor, generacion, amount, token);
        return amount;
    }

    function alimentarPool(
        mapping(address => uint256) storage totalLiquidity,
        IERC20 token,
        address tokenAddr,
        uint256 amount
    ) external {
        require(amount == 5e17 || amount == 1e18);
        totalLiquidity[tokenAddr] += amount;
        token.safeTransferFrom(msg.sender, address(this), amount);
    }

    function recoverAttest(
        address user,
        bytes32 phoneHash,
        bytes32 deviceHash,
        uint256 deadline,
        uint256 nonce,
        uint8 v,
        bytes32 r,
        bytes32 s
    ) external view returns (address recovered) {
        if (v != 27 && v != 28) return address(0);
        if (uint256(s) > 0x7FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF5D576E7357A4501DDFE92F46681B20A0) {
            return address(0);
        }
        bytes32 packed = keccak256(
            abi.encode(user, phoneHash, deviceHash, deadline, nonce, block.chainid, address(this))
        );
        recovered = ecrecover(
            keccak256(abi.encodePacked("\x19Ethereum Signed Message:\n32", packed)),
            v,
            r,
            s
        );
    }

    /// @notice Alta directa solo si no hay contrato Alta. Si Alta llama, el usuario es quien firmó,
    /// salvo cargoDe(), que apunta al deudor cuando vence el padrino.
    function usuarioVerificacion(address hermano, address caller, address origin)
        external
        view
        returns (address user)
    {
        (bool okA, bytes memory rawA) = hermano.staticcall(abi.encodeWithSignature("alta()"));
        address alta = (okA && rawA.length >= 32) ? abi.decode(rawA, (address)) : address(0);
        if (origin != caller) {
            require(alta == caller);
            user = origin;
            if (alta != address(0)) {
                (bool okU, bytes memory rawU) = alta.staticcall(abi.encodeWithSignature("cargoDe()"));
                if (okU && rawU.length >= 32) {
                    address cargo = abi.decode(rawU, (address));
                    if (cargo != address(0)) user = cargo;
                }
            }
            return user;
        }
        require(alta == address(0));
        return caller;
    }

    /// @notice Si Fama tiene Alta, el préstamo exige el pago de 4 USDT (registroHecho).
    function requireAltaPagada(address hermano, address user) external view {
        (bool okA, bytes memory rawA) = hermano.staticcall(abi.encodeWithSignature("alta()"));
        if (!(okA && rawA.length >= 32)) return;
        address alta = abi.decode(rawA, (address));
        if (alta == address(0)) return;
        (bool ok, bytes memory data) = alta.staticcall(
            abi.encodeWithSignature("registroHecho(address)", user)
        );
        require(ok && data.length >= 32 && abi.decode(data, (bool)));
    }

    function assertPeg(AggregatorV3Interface feed) external view {
        require(address(feed) != address(0));
        (
            uint80 roundId,
            int256 price,
            uint256 startedAt,
            uint256 updatedAt,
            uint80 answeredInRound
        ) = feed.latestRoundData();
        require(price > 0);
        uint8 dec = feed.decimals();
        require(dec >= 2 && dec <= 18);
        int256 threshold = int256(uint256(98) * (10 ** uint256(dec - 2)));
        require(price >= threshold);
        require(updatedAt > 0 && block.timestamp - updatedAt <= 30 minutes);
        require(roundId > 0 && answeredInRound == roundId);
        require(startedAt > 0 && startedAt <= updatedAt);
    }
}
